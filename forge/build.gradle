plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    neoForge {
        platformPackage = "forge"
    }
}


loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}


processResources {
    // input variables of this task:
    var replaceProperties = [
            minecraft_version    : minecraft_version,
            minecraft_min_version: minecraft_min_version,
            minecraft_max_version: minecraft_max_version,
            pack_format_number   : pack_format_number,

            mod_id               : mod_id,
            mod_name             : mod_name,
            mod_version          : mod_version,
            mod_license          : mod_license,
            mod_authors          : mod_authors,
            mod_description      : mod_description,
            mod_credits          : mod_credits,
            mod_homepage         : mod_homepage,
            mod_github           : mod_github,

            neo_version          : neo_version,
            neo_version_range    : neo_version_range,
            loader_version_range : loader_version_range,
    ]
    // setting them as input
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta', 'update.json']) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.mod_id + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        maven {
            url = "https://www.cursemaven.com"
            allowInsecureProtocol = true
        }
    }
}


// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }


repositories{
    // Built mods
    flatDir {
        dirs "mods"
    }
    mavenCentral()
}

dependencies {
    //forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    neoForge "net.neoforged:neoforge:${neo_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    modImplementation ("curse.maven:jei-238222:5466551")
    modImplementation("curse.maven:emi-580555:5436759")

    modCompileOnly("curse.maven:roughly-enough-items-310111:5435024")

    //modRuntimeOnly("curse.maven:roughly-enough-items-hacks-521393:4402999")
    modCompileOnly("curse.maven:architectury-api-419699:4581905")
    modCompileOnly("curse.maven:stylish-effects-543661:4582073")
    modCompileOnly("curse.maven:puzzles-lib-495476:4596893")
    //modRuntimeOnly("curse.maven:create-328085:4835191")
    //modRuntimeOnly("com.jozufozu.flywheel:flywheel-forge-1.20.1-0.6.10-7")
    //modRuntimeOnly("com.tterrag.registrate:Registrate:MC1.20-1.3.3")
    //modImplementation ("curse.maven:jei-238222:3884337")
    //modImplementation ("curse.maven:configured-457570:4011355")

    modCompileOnly("curse.maven:farmers-delight-398521:5051242")

}
